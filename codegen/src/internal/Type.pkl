// ===----------------------------------------------------------------------===//
// Copyright Â© 2024 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//	https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
// ===----------------------------------------------------------------------===//
/// Representation of a type in Python.
@Unlisted
abstract module pkl.python.internal.Type

import "Type.pkl"

/// The imports required by this type.
imports: List<String>

/// True if this type is polymorphic (a protocol, not a struct)
isPolymorphic: Boolean = false

/// True if this type represents a Pkl Any
isAny: Boolean = false

/// Renders this type into source code.
///
/// [pythonModuleName] is the full path of the package that this type appears in.
abstract function render(pythonModuleName: String?): String

/// Renders this type into source code.
/// Polymorphic types will render as PklAny.
///
/// [pythonModuleName] is the full path of the package that this type appears in.
abstract function renderGeneric(pythonModuleName: String?): String

abstract function renderForwardSafe(pythonModuleName: String?): String

/// Renders this type into source code.
///
/// [namespaceName] is the full path of the package that this type appears in.
abstract function renderImports(namespaceName: String?): List<String>

class Dictionary extends Type {
  key: Type

  elem: Type

  imports = key.imports + elem.imports

  isPolymorphic = key.isPolymorphic || elem.isPolymorphic

  isAny = key.isAny || elem.isAny

  function render(withinNamespace: String?) =
    /*
    let (renderedKey = if (key.isPolymorphic) throw("""
        Illegal: Polymorphic key `\(key.render(withinNamespace))` is not supported when generating Python.
        """) else key.render(withinNamespace))
    */
    let (renderedKey = key.render(withinNamespace))
    "Dict[\(renderedKey), \(elem.render(withinNamespace))]"

  function renderForwardSafe(withinNamespace: String?) =
    let (renderedKey = key.renderForwardSafe(withinNamespace))
    "Dict[\(renderedKey), \(elem.render(withinNamespace))]"

  function renderGeneric(withinNamespace: String?) =
    "[\(key.renderGeneric(withinNamespace)): \(elem.renderGeneric(withinNamespace))]"

  function renderImports(withinNamespace: String?) =
    key.renderImports(withinNamespace) + elem.renderImports(withinNamespace)
}

class Array extends Type {
  elem: Type

  imports = elem.imports

  isPolymorphic = elem.isPolymorphic

  isAny = elem.isAny

  function render(withinNamespace: String?) = "List[\(elem.render(withinNamespace))]"

  function renderForwardSafe(withinNamespace: String?) = "List[\(elem.renderForwardSafe(withinNamespace))]"

  function renderGeneric(withinNamespace: String?) = "List[\(elem.renderGeneric(withinNamespace))]"

  function renderImports(withinNamespace: String?) = elem.renderImports(withinNamespace)
}

class Nullable extends Type {
  elem: Type

  isPolymorphic = elem.isPolymorphic

  isAny = elem.isAny

  function render(withinNamespace: String?) =
    /*
    if (elem.isPolymorphic) "(\(elem.render(withinNamespace)))?"
    else "Optional[\(elem.render(withinNamespace))]"
    */
    "Optional[\(elem.render(withinNamespace))]"

  function renderForwardSafe(withinNamespace: String?) =
    "Optional[\(elem.renderForwardSafe(withinNamespace))]"

  function renderGeneric(withinNamespace: String?) =
    let (rendered = elem.renderGeneric(withinNamespace))
    if (rendered == "PklPython.PklAny")
      rendered
    else
      "Optional[\(rendered)]"

  function renderImports(withinNamespace: String?) = elem.renderImports(withinNamespace)
}

class Tuple extends Type {
  members: Listing<Type>

  isPolymorphic = members.toList().any((t) -> t.isPolymorphic)

  isAny = members.toList().any((t) -> t.isAny)

  function render(withinNamespace: String?) =
    "(" + members.toList().map((it) -> it.render(withinNamespace)).join(", ") + ")"

  function renderForwardSafe(withinNamespace: String?) =
    "(" + members.toList().map((it) -> it.renderForwardSafe(withinNamespace)).join(", ") + ")"

  function renderGeneric(withinNamespace: String?) =
    "(" + members.toList().map((it) -> it.renderGeneric(withinNamespace)).join(", ") + ")"

  function renderImports(withinNamespace: String?) =
    members.flatMap((it)-> it.renderImports(withinNamespace))
}

class Union extends Type {
  members: List<Type>

  isPolymorphic = members.toList().any((t) -> t.isPolymorphic)

  isAny = members.toList().any((t) -> t.isAny)

  function render(withinNamespace: String?) =
    "Union[" + members.toList().map((it) -> it.render(withinNamespace)).join(", ") + "]"

  function renderForwardSafe(withinNamespace: String?) =
    "Union[" + members.toList().map((it) -> it.renderForwardSafe(withinNamespace)).join(", ") + "]"

  function renderGeneric(withinNamespace: String?) =
    "Union[" + members.toList().map((it) -> it.renderGeneric(withinNamespace)).join(", ") + "]"

  function renderImports(withinNamespace: String?) =
    members.flatMap((it)-> it.renderImports(withinNamespace))
}

class StringLiteral extends Type {
  values: List<String>

  isPolymorphic = members.toList().any((t) -> t.isPolymorphic)

  isAny = members.toList().any((t) -> t.isAny)

  function render(withinNamespace: String?) =
    "Literal[\"" + values.join("\", \"") + "\"]"

  function renderForwardSafe(withinNamespace: String?) = render(withinNamespace)

  function renderGeneric(withinNamespace: String?) =
    "Literal[\"" + values.join("\", \"") + "\"]"

  function renderImports(withinNamespace: String?) = List()
}

class Declared extends Type {
  /// The name of the namespace that contains this type.
  ///
  /// Namespaces are empty enums and an extension.
  namespaceName: String?

  /// The name of the Python module that contains this type.
  pythonModuleName: String?

  /// The name of the type
  typeName: String

  /// The type arguments, if any.
  typeArguments: List<Type>?

  isUserDefined: Boolean = false

  function getUserDefined() =
    ( this ) {isUserDefined = true}

  isAny = typeName == "AnyHashable"
    && pythonModuleName == null
    && namespaceName == null

  function renderImports(withinNamespace: String?) =
    // Always qualify imported type names so we avoid conflicts.
    if (namespaceName != null && withinNamespace != this.namespaceName)
      List("from . import " + this.namespaceName+ "_pkl")
    else
      List()

  function renderBase(withinNamespace: String?) =
    // Always qualify imported type names so we avoid conflicts.
    /*
    let (prefix = if (this.isPolymorphic) "any " else "")
    prefix + if (namespaceName != null && withinNamespace != this.namespaceName)
        "\(namespaceName).\(typeName)"
    else typeName
    */
    if (namespaceName != null && withinNamespace != this.namespaceName)
      "\(namespaceName)_pkl.\(typeName)"
    /*
    else if ( pythonModuleName != null )
      let(_ = trace(List(namespaceName, withinNamespace, pythonModuleName)))
      "\(pythonModuleName).\(typeName)"
    */
    else
      typeName

  function renderBaseSafe(withinNamespace: String?) =
    // Always qualify imported type names so we avoid conflicts.
    /*
    let (prefix = if (this.isPolymorphic) "any " else "")
    prefix + if (namespaceName != null && withinNamespace != this.namespaceName)
        "\(namespaceName).\(typeName)"
    else typeName
    */
    if (namespaceName != null && withinNamespace != this.namespaceName)
      "\(namespaceName)_pkl.\(typeName)"
    /*
    else if ( pythonModuleName != null )
      "\(pythonModuleName).\(typeName)"
    */
    else if ( isUserDefined )
      "\"\(typeName)\""
    else
      typeName

  function renderTypeArguments(withinNamespace: String?) =
    if (typeArguments == null) ""
    else "[" + typeArguments.map((t) -> t.render(withinNamespace)).join(", ") + "]"

  function render(withinNamespace: String?) =
    renderBase(withinNamespace) + renderTypeArguments(withinNamespace)

  function renderForwardSafe(withinNamespace: String?) =
    renderBaseSafe(withinNamespace) + renderTypeArguments(withinNamespace)

  function renderBaseGeneric(withinNamespace: String?) =
    // Always qualify imported type names so we avoid conflicts.
    let (name = if (this.isPolymorphic || typeName == "AnyHashable") "PklPython.PklAny" else typeName)
    if (!this.isPolymorphic && namespaceName != null && withinNamespace != this.namespaceName)
      "\(namespaceName).\(name)"
    else name

  function renderTypeArgumentsGeneric(withinNamespace: String?) =
    if (typeArguments == null) ""
    else "<" + typeArguments.map((t) -> t.renderGeneric(withinNamespace)).join(", ") + ">"

  function renderGeneric(withinNamespace: String?) =
    renderBaseGeneric(withinNamespace) + renderTypeArgumentsGeneric(withinNamespace)
}
